#Maintainer: Michel Blanc <mblanc@erasme.org>
pkgname=ansible-git
pkgver=1.1.568.gd29d142
pkgver(){
	cd "$srcdir/$pkgname"
	git describe --tags --long | sed 's/^v//;s/-/./g'
}
pkgrel=1
pkgdesc="A radically simple deployment, model-driven configuration management, and command execution framework"
arch=('any')
url="http://ansible.cc"
license=('GPL3')
depends=('python2' 'python2-paramiko' 'python2-jinja' 'python2-yaml')
makedepends=('git' 'asciidoc' 'fakeroot')
optdepends=('python2-pyzmq: needed for fireball mode'
	'python2-pyasn1: needed for fireball mode'
	'python2-crypto: needed for fireball mode'
	'python2-keyczar: needed for fireball mode')
conflicts=('ansible')
source=("$pkgname::git://github.com/ansible/ansible.git"
		"python-binary.diff")
md5sums=("SKIP" "ab81876d8d4c86c27d137e3d86c25a3a")

prepare(){
  cd "$srcdir/$pkgname"
  patch -p1 -i "$srcdir/python-binary.diff"
}
build() {
  cd "$srcdir/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname"

  mkdir -p "${pkgdir}/usr/share/ansible"
  cp -dpr --no-preserve=ownership ./library/* "${pkgdir}/usr/share/ansible/"
  cp -dpr --no-preserve=ownership ./examples "${pkgdir}/usr/share/ansible"

  python2 setup.py install -O1 --root="$pkgdir"

  install -D docs/man/man1/ansible.1 "${pkgdir}/usr/share/man/man1/ansible.1"
  install -D docs/man/man1/ansible-playbook.1 "${pkgdir}/usr/share/man/man1/ansible-playbook.1"
  install -D docs/man/man1/ansible-pull.1 "${pkgdir}/usr/share/man/man1/ansible-pull.1"
  install -D docs/man/man1/ansible-doc.1 "${pkgdir}/usr/share/man/man1/ansible-doc.1"

  gzip -9 "${pkgdir}/usr/share/man/man1/ansible.1"
  gzip -9 "${pkgdir}/usr/share/man/man1/ansible-playbook.1"
  gzip -9 "${pkgdir}/usr/share/man/man1/ansible-pull.1"
  gzip -9 "${pkgdir}/usr/share/man/man1/ansible-doc.1"
}
